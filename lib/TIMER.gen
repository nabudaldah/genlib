FUNCTION TIMER_HELP
  INFOLINE 1 ""
  INFOLINE 1 "Timer Library v1 - Profiling toolset (millisecond precision)"
  INFOLINE 1 ""
  INFOLINE 1 "Functions:"
  INFOLINE 1 ""
  INFOLINE 1 "  TIMER_START label          Start the clock!"
  INFOLINE 1 "  TIMER_LAP label            Show elapsed time, optionally with label prefix."
  INFOLINE 1 "  TIMER_RELAP label          Show elapsed time, optionally with label prefix, and restart the clock."
  INFOLINE 1 ""
  INFOLINE 1 "Globals:"
  INFOLINE 1 ""
  INFOLINE 1 "  TIMER_CLOCK                Timer clock in days (from NOW function)"
  INFOLINE 1 ""
  INFOLINE 1 "Example:"
  INFOLINE 1 ""
  INFOLINE 1 "  1 INCLUDE ~"TIMER~""
  INFOLINE 1 "  2 TIMER_START"
  INFOLINE 1 "  3 SLEEP 1000"
  INFOLINE 1 "  4 TIMER_LAP ~"Operation took:~""
  INFOLINE 1 ""
ENDFUNCTION

# Global timer variable
VAR TIMER_CLOCK
DEF TIMER_CLOCK = NOW

# Start the timer!
FUNCTION TIMER_START label

  DEF TIMER_CLOCK = NOW
  
  IF LENGTH(%label$%) > 0
    INFOLINE 1 %label$%
  ENDIF

ENDFUNCTION

# Print elapsed time since script start or TIMER_START call
FUNCTION TIMER_LAP label

  # Stop the clock!
  VAR time
  DEF time = NOW - %TIMER_CLOCK%

  # Define different time units
  VAR days hours minutes seconds mseconds
  DEF days     = %time%
  DEF hours    = %time%    * 24
  DEF minutes  = %hours%   * 60
  DEF seconds  = %minutes% * 60
  DEF mseconds = %seconds% * 1000

  # Round the numbers and get modulo
  DEF days     = FLOOR(%days%)
  DEF hours    = FLOOR(%hours%)    MOD 24
  DEF minutes  = FLOOR(%minutes%)  MOD 60
  DEF seconds  = FLOOR(%seconds%)  MOD 60
  DEF mseconds = FLOOR(%mseconds%) MOD 1000

  # Create timer string (optionally with label)
  VAR str t[] i
  IF %label$% != ""
    DEF str = %label$% + " "
  ENDIF
  IF %days% > 0
    DEF str = %str$% + %days$% + "d "
  ENDIF
  IF %hours% > 0
    DEF str = %str$% + %hours$% + "h "
  ENDIF
  IF %minutes% > 0
    DEF str = %str$% + %minutes$% + "m "
  ENDIF
  IF %seconds% > 0
    DEF str = %str$% + %seconds$% + "s "
  ENDIF
  IF %mseconds% > 0
    DEF str = %str$% + %mseconds$% + "ms "
  ENDIF

  DEF str = TRIM(%str$%)

  INFOLINE 1 %str$%

ENDFUNCTION

# Timer lapse and start again
FUNCTION TIMER_RELAP label
  TIMER_LAP %label$%
  TIMER_START
ENDFUNCTION

# Example:

#TIMER_START "First lap!"
#SLEEP 100
#TIMER_LAP "First lap:"
#SLEEP 100
#TIMER_LAP "Second lap:"
#SLEEP 100
#TIMER_RELAP "Final lap:"
#SLEEP 1000
#TIMER_RELAP "New lap:"
#SLEEP 1000
#TIMER_RELAP "Another new lap:"


